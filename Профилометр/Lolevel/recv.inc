;*******************************************************************************
; Модуль приема данных из COM-порта 9600 бит/с, 8 бит данных, 1 стоп-бит
; процедуры:
;  receive_char_wait_proc
;*******************************************************************************

;-------------------------------------------------------------------------------
; Процедура получения символа из порта
; Ожидает поступления символа
; Символ сохраняется в регистре a, если не принят, то в a - мусор
;-------------------------------------------------------------------------------
receive_char_wait_proc:

waiting_start_bit:
		in a,(RX)	;11	; стартовый бит
                rra             ;4      ; символа

                call delay_107	;107	; задержка

                jp c,waiting_start_bit   ;10

		
                call delay_107	;107
                call delay_107	;107	; задержка
                call delay_39	;39
                call delay_27   ;27

		ld b,8          ;7      ; 8 бит
receive_bits_loop:
		in a,(RX)  	;11     ; очередной бит символа
                rra             ;4
                rr d            ;8

                call delay_107	;
                call delay_107	;
                call delay_39	;
		call delay_39	;
                call delay_27   ;       ; задержка
                call delay_27   ;
                call delay_27   ;
                nop             ;
                nop             ;381

                dec b           ;4      ; счетчик принимаемых бит
                jp nz,receive_bits_loop ;10


                in a,(RX)        ;11   ; стоповый бит
                rra               ;4
                jp nc,receive_err ;10

                ld a,d            ;4

receive_err:

		ret